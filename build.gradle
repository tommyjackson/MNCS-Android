import org.apache.tools.ant.taskdefs.condition.Os

buildscript {
    ext {
        // kotlin
        kotlinVersion = "1.5.31"

        // hilt
        hiltVersion = "2.40.1"

        // android gradle plugin
        agpVersion = "7.0.4"

        // spotless
        spotlessVersion = "6.0.0"
    }

    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:$agpVersion")
        classpath("com.google.dagger:hilt-android-gradle-plugin:$hiltVersion")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion")
        classpath("com.diffplug.spotless:spotless-plugin-gradle:$spotlessVersion")
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task installGitHook(type: Copy) {
    def suffix = "macos"
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        suffix = "windows"
    }
    from new File(rootProject.rootDir, "scripts/pre-commit-$suffix")
    into { new File(rootProject.rootDir, ".git/hooks") }
    rename("pre-commit-$suffix", "pre-commit")
    fileMode 0775
}

tasks.getByPath(":app:preBuild").dependsOn installGitHook